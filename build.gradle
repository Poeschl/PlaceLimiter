import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    //noinspection GroovyAssignabilityCheck
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'net.researchgate.release' version '2.4.0'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.sk89q.com/repo/" }
}

group = 'io.github.poeschl.bukkit'

description = """PlaceLimiter"""

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
targetCompatibility = 1.7

//noinspection GroovyAssignabilityCheck
processResources {
    filter(ReplaceTokens, tokens: [
            "name"   : project.name,
            "version": project.version
    ])
}

//noinspection GroovyAssignabilityCheck
jar {
    manifest {
        attributes("Project": project.name,
                "Version": project.version)
    }
}

shadowJar {
    configurations = [project.configurations.compile]
    classifier = null
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["**/InstanceFactory.*"])
        })
    }
}

release {
    preTagCommitMessage = '[Gradle Release Plugin] - Version: '
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compileOnly 'org.bukkit:bukkit:1.7.9-R0.2'
    compile 'com.squareup.moshi:moshi:1.1.0'
    compile 'com.squareup.okio:okio:1.8.0'

    testCompile 'org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion'
    testCompile 'org.bukkit:bukkit:1.7.9-R0.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'org.mockito:mockito-core:2.2.15'
    testCompile 'com.nhaarman:mockito-kotlin:0.11.0'
    //TODO: Waiting for mokito 2 support of powermock
//    testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
//    testCompile "org.powermock:powermock-api-mockito2:$powerMockVersion"
}
